plugins {
    id 'org.jetbrains.kotlin.multiplatform'
    id 'base'
}

repositories {
    mavenCentral()
}

def GROUP_ID = 'net.aquadc.collections'
def ARTIFACT_ID = rootProject.name
def LIBRARY_VERSION_NAME = '1.0-alpha05'

group GROUP_ID
version LIBRARY_VERSION_NAME

apply plugin: 'maven-publish'

kotlin {
    jvm()
    js {
//        browser {} // requires Chrome-headless to run tests
        nodejs {}
    }
    // For ARM, should be changed to iosArm32 or iosArm64
    // For Linux, should be changed to e.g. linuxX64
    // For MacOS, should be changed to e.g. macosX64
    // For Windows, should be changed to e.g. mingwX64
    linuxX64("linux")
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        noJvmMain {
        }
        jsMain {
            dependsOn noJvmMain
            dependencies {
                implementation kotlin('stdlib-js')
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
        linuxMain {
            dependsOn noJvmMain
        }
        linuxTest {}
    }
}

afterEvaluate {
    project.publishing.publications.all {
        // rename artifacts
        groupId = GROUP_ID
        if (it.name.contains('metadata')) {
            artifactId = "$ARTIFACT_ID"
        } else {
            artifactId = "$ARTIFACT_ID-$name"
        }
    }
}
